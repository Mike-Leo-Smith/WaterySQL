Stmt                ->  SHOW ShowStmtBody ';'                               // done
                    |   CREATE CreateStmtBody ';'                           // done
                    |   DROP DropStmtBody ';'                               // done
                    |   USE IDENTIFIER ';'                                  // done
                    |   DESCRIBE IDENTIFIER ';'                             // done
                    |   INSERT INTO IDENTIFIER VALUES ValueTupleList ';'    // done
                    |   DELETE FROM IDENTIFIER WhereClause ';'
                    |   UPDATE IDENTIFIER SetClause WhereClause ';'
                    |   SELECT Selector FROM TableList WhereClause ';'

ShowStmtBody        ->  DATABASES                                           // done
                    |   TABLES                                              // done

CreateStmtBody      ->  DATABASE IDENTIFIER                                 // done
                    |   TABLE IDENTIFIER '(' FieldList ')'                  // done
                    |   INDEX IDENTIFIER '(' IDENTIFIER ')'                 // done

DropStmtBody        ->  DATABASE IDENTIFIER                                 // done
                    |   TABLE IDENTIFIER                                    // done
                    |   INDEX IDENTIFIER '(' IDENTIFIER ')'                 // done

FieldList           ->  Field FieldListTail                                 // done with hack

FieldListTail       ->  ',' FieldList                                       // done with hack
                    |   // empty, lookahead == ')'                          // done with hack

Field               ->  IDENTIFIER Type NullableHint                        // done
                    |   PRIMARY KEY '(' IDENTIFIER ')'                      // done
                    |   FOREIGN KEY '(' IDENTIFIER ')' REFERENCES
                          IDENTIFIER '(' IDENTIFIER ')'                     // done
                    |   UNIQUE '(' IDENTIFIER ')'                           // done

Type                ->  INT                                                 // done
                    |   VARCHAR                                             // done
                    |   DATE                                                // done
                    |   FLOAT                                               // done

SizeHint            ->  '(' NUMBER ')'                                      // done
                    |   // empty, lookahead in {NOT, NUL, ',', ')'}         // done

NullableHint        ->  NOT NULL                                            // done
                    |   NULL                                                // done
                    |   // empty, lookahead == ',' || lookahead == ')'      // done

ValueTupleList      ->  '(' ValueTuple ')' ValueTupleListTail               // done with hack

ValueTupleListTail  ->  ',' ValueTupleList                                  // done
                    |   // empty, lookahead == ';'                          // done

ValueTuple          ->  Value ValueTupleTail                                // done with hack

ValueTupleTail      ->  ',' ValueTuple                                      // done
                    |   // empty, lookahead == ')'                          // done

Value               ->  NUMBER                                              // done but parsed as raw string
                    |   STRING                                              // done
                    |   NULL                                                // done

WhereClause         ->  WHERE WhereClauseBody
                    |   // empty, lookahead == ';'

WhereClauseBody     ->  Column ColumnPredicate Conjunction

ColumnPredicate     ->  OPERATOR Expr
                    |   IS NullOrNot

IsNullOrNot         ->  NULL
                    |   NOT NULL

Conjunction         ->  AND WhereClauseBody
                    |   // empty, lookahead == ';'

Column              ->  IDENTIFIER ColumnDomain

ColumnDomain        ->  '.' IDENTIFIER
                    |   // empty, lookahead == OPERATOR || lookahead == IS

Expr                ->  Value
                    |   Column

SetClause           ->  SET SetClauseBody

SetClauseBody       ->  IDENTIFIER '=' Value SetClauseTail
                    
SetClauseTail       ->  ',' SetClauseBody
                    |   // empty, lookahead == WHERE || lookahead == ';'

Selector            ->  '*'
                    |   IDENTIFIER ColumnDomain SelectorTail

SelectorTail        ->  ',' Column SelectorTail
                    |   // empty, lookahead == FROM

TableList           ->  IDENTIFIER TableListTail

TableListTail       ->  ',' TableList
                    |   // empty, lookahead == WHERE || lookahead == ';'
