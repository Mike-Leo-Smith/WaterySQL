Stmt                ->  SHOW ShowStmtBody ';'       // done
                    |   CREATE CreateStmtBody ';'
                    |   DROP DropStmtBody ';'       // done
                    |   USE IDENTIFIER ';'          // done
                    |   DESC IDENTIFIER ';'         // done
                    |   INSERT INTO IDENTIFIER VALUES ValueListList ';'
                    |   DELETE FROM IDENTIFIER WHERE WhereClause ';'
                    |   UPDATE IDENTIFIER SET SetClause WHERE WhereClause ';'
                    |   SELECT Selector FROM TableList WHERE WhereClause ';'

ShowStmtBody        ->  DATABASES   // done
                    |   TABLES      // done

CreateStmtBody      ->  DATABASE IDENTIFIER                     // done
                    |   TABLE IDENTIFIER '(' FieldList ')'      // done
                    |   INDEX IDENTIFIER '(' IDENTIFIER ')'     // done

DropStmtBody        ->  DATABASE IDENTIFIER                     // done
                    |   TABLE IDENTIFIER                        // done
                    |   INDEX IDENTIFIER '(' IDENTIFIER ')'     // done

FieldList           ->  Field FieldListTail

FieldListTail       ->  ',' FieldList
                    |   // empty, lookahead == ')'

Field               ->  IDENTIFIER Type                     // done
                    |   PRIMARY KEY '(' ColumnList ')'
                    |   FOREIGN KEY '(' IDENTIFIER ')' REFERENCES IDENTIFIER '(' IDENTIFIER ')'

Type                ->  INT '(' NUMBER ')' NullableNotation         // done
                    |   VARCHAR '(' NUMBER ')' NullableNotation     // done
                    |   DATE NullableNotation                       // done
                    |   FLOAT NullableNotation                      // done

NullableNotation    ->  NOT NULL                                        // done
                    |   // empty, lookahead == ',' || lookahead == ')'  // done

ValueListList       ->  '(' ValueList ')' ValueListListTail

ValueListListTail   ->  ',' ValueListList
                    |   // empty, lookahead == ';'

ValueList           ->  Value ValueListTail

ValueListTail       ->  ',' ValueList

Value               ->  NUMBER
                    |   STRING
                    |   NUL

WhereClause         ->  Column ColumnPredicate Conjunction

ColumnPredicate     ->  OPERATOR Expr
                    |   IS NullOrNot

IsNullOrNot         ->  NULL
                    |   NOT NULL

Conjunction         ->  AND WhereClause
                    |   // empty, lookahead == ';'

Column              ->  IDENTIFIER ColumnDomain

ColumnDomain        ->  '.' IDENTIFIER
                    |   // empty, lookahead == OPERATOR || lookahead == IS

Expr                ->  Value
                    |   Column

SetClause           ->  IDENTIFIER '=' Value SetClauseTail
                    
SetClauseTail       ->  ',' SetClause
                    |   // empty, lookahead = WHERE

Selector            ->  '*'
                    |   IDENTIFIER ColumnDomain SelectorTail

SelectorTail        ->  ',' Column SelectorTail
                    |   // empty, lookahead = FROM

TableList           ->  IDENTIFIER TableListTail

TableListTail       ->  ',' TableList
                    |   // empty, lookahead == WHERE

ColumnList          ->  IDENTIFIER ColumnListTail

ColumnListTail      ->  ',' ColumnList
                    |   // empty, lookahead == ')'
