Stmt                ->  SHOW ShowStmtBody ';'                               // done
                    |   CREATE CreateStmtBody ';'                           // done
                    |   DROP DropStmtBody ';'                               // done
                    |   USE IDENTIFIER ';'                                  // done
                    |   DESCRIBE IDENTIFIER ';'                             // done
                    |   INSERT INTO IDENTIFIER VALUES ValueTupleList ';'    // done
                    |   DELETE FROM IDENTIFIER WhereClause ';'              // done
                    |   UPDATE IDENTIFIER SetClause WhereClause ';'         // done
                    |   SELECT Selector FROM TableList WhereClause ';'

ShowStmtBody        ->  DATABASES                                           // done
                    |   TABLES                                              // done

CreateStmtBody      ->  DATABASE IDENTIFIER                                 // done
                    |   TABLE IDENTIFIER '(' FieldList ')'                  // done
                    |   INDEX IDENTIFIER '(' IDENTIFIER ')'                 // done

DropStmtBody        ->  DATABASE IDENTIFIER                                 // done
                    |   TABLE IDENTIFIER                                    // done
                    |   INDEX IDENTIFIER '(' IDENTIFIER ')'                 // done

FieldList           ->  Field FieldListTail                                 // done with hack

FieldListTail       ->  ',' FieldList                                       // done with hack
                    |   // empty, lookahead == ')'                          // done with hack

Field               ->  IDENTIFIER Type SizeHint NullableHint               // done
                    |   PRIMARY KEY '(' IDENTIFIER ')'                      // done
                    |   FOREIGN KEY '(' IDENTIFIER ')' REFERENCES
                          IDENTIFIER '(' IDENTIFIER ')'                     // done
                    |   UNIQUE '(' IDENTIFIER ')'                           // done
                    |   CHECK '(' IDENTIFIER IN '(' ValueTuple ')' ')'

Type                ->  INT                                                 // done
                    |   VARCHAR                                             // done
                    |   DATE                                                // done
                    |   FLOAT                                               // done

SizeHint            ->  '(' NUMBER ')'                                      // done
                    |   // empty, lookahead in {NOT, NUL, ',', ')'}         // done

NullableHint        ->  NOT NULL                                            // done
                    |   NULL                                                // done
                    |   // empty, lookahead == ',' || lookahead == ')'      // done

ValueTupleList      ->  '(' ValueTuple ')' ValueTupleListTail               // done with hack

ValueTupleListTail  ->  ',' ValueTupleList                                  // done
                    |   // empty, lookahead == ';'                          // done

ValueTuple          ->  Value ValueTupleTail                                // done with hack

ValueTupleTail      ->  ',' ValueTuple                                      // done
                    |   // empty, lookahead == ')'                          // done

Value               ->  NUMBER                                              // done but parsed as raw string
                    |   STRING                                              // done
                    |   NULL                                                // done

WhereClause         ->  WHERE WhereClauseBody                               // done
                    |   // empty, lookahead == ';'                          // done

WhereClauseBody     ->  Column ColumnPredicate Conjunction                  // done

ColumnPredicate     ->  OPERATOR Value                                      // done
                    |   IS NullOrNot                                        // done

IsNullOrNot         ->  NULL                                                // done
                    |   NOT NULL                                            // done

Conjunction         ->  AND WhereClauseBody                                 // done
                    |   // empty, lookahead == ';'                          // done

Column              ->  IDENTIFIER ColumnDomain                             // done with hack

ColumnDomain        ->  '.' IDENTIFIER                                      // done
                    |   // empty, lookahead == OPERATOR || lookahead == IS  // done

SetClause           ->  SET SetClauseBody                                   // done

SetClauseBody       ->  IDENTIFIER '=' Value SetClauseTail                  // done

SetClauseTail       ->  ',' SetClauseBody                                   // done
                    |   // empty, lookahead == WHERE || lookahead == ';'    // done

Selector            ->  '*'                                                 // done
                    |   Column SelectorTail                                 // done with hack

SelectorTail        ->  ',' Column SelectorTail                             // done
                    |   // empty, lookahead == FROM                         // done

TableList           ->  IDENTIFIER TableListTail                            // done with hack

TableListTail       ->  ',' TableList                                       // done
                    |   // empty, lookahead == WHERE || lookahead == ';'    // done
